
*
*
* ELEVATOR AND DOOR SCRIPT
*
* This script inject the elevators and doors.
*


*
* ====== INJECTING ELEVINES ======
*
**Upper
new: simp 3 1 36461 "fix_elevine" 4 0 1
* attr: activateable (4) + collision (64) + physics (128) = 196
* bhvr: act1 (1) + act2 (2) = 3
attr 196
bhvr 3
perm 100
fric 100
elas 0
accg 1
aero 1
core 0 80 0 50
mvsf 26628 518

**Mid
new: simp 3 1 36461 "fix_elevine" 4 0 1
* attr: activateable (4) + collision (64) + physics (128) = 196
* bhvr: act1 (1) + act2 (2) = 3
attr 196
bhvr 3
perm 100
fric 100
elas 0
accg 1
aero 1
core 0 80 0 50
mvsf 26628 910

**Lower
new: simp 3 1 36461 "fix_elevine" 4 0 1
* attr: activateable (4) + collision (64) + physics (128) = 196
* bhvr: act1 (1) + act2 (2) = 3
attr 196
bhvr 3
perm 100
fric 100
elas 0
accg 1
aero 1
core 0 80 0 50
mvsf 26628 1231

*
* ====== INJECTING DOORS ======
*
** DOOR INSIDE METAROOM
new: simp 2 2 36461 "fix_door" 2 0 300
* attr: activateable (4) + collision (64) + physics (128) = 196
* bhvr: act1 (1)
attr 196
bhvr 1
perm 100
fric 100
elas 0
accg 1
aero 1
setv ov01 1
mvto 26409 879

** INJECTED DOOR
new: simp 2 2 36461 "fix_door" 2 0 300
* attr: mouseable (2), activateable (4), collide (64) and physics (128) = 198
* bhvr: act1 (1)
attr 198
bhvr 1
accg 1
aero 0
fric 60
elas 30
setv ov01 1
mvsf game "CreatorX" game "CreatorY"

*
* ====== CA LINKS ======
*

* Elevine CAs
setv va01 grap 26628 601
setv va02 grap 26628 959
setv va03 grap 26628 1272
link va01 va02 40
link va02 va03 40

* Air connection
setv va01 grap 27006 283
setv va02 grap 27006 407
link va01 va02 100
setv va01 grap 27753 985
setv va02 grap 27753 1077
link va01 va02 100

* Water connection
setv va01 grap 28131 680
setv va02 grap 28131 750
link va01 va02 100

*
* ====== SCRIPTS ======
*

*
** DOOR SCRIPT

** BUG: If one of the doors is in the inventory and offscreen, this throws an error.
** For all you amazing fixer-uppers: feel free to fix this bug. ;) <-- I fixed it Pilla, for real this time :D
** Thanks to Verm for finding the bug!


scrp 2 2 36461 1
	anim [1 0 1 0]
	over
	setv va00 ov01
	seta va01 ownr


	enum 2 2 36461
		doif targ ne va01
			doif ov01 eq va00
				doif carr ne null or fall eq 1
					doif targ ne null
						snde "buzz"
						doif carr ne null
							rtar 1 2 10
							mesg wrt+ targ 126 "Destination is being carried" ownr 0
							stop
						endi
						doif fall eq 1
							rtar 1 2 10
							mesg wrt+ targ 126 "Destination is falling" ownr 0
							stop
						endi
					endi
				endi

				setv va02 posx
				setv va03 posy
				setv va04 posb

				doif from eq pntr
					cmrp va02 va03 0
					etch 4 0 0
						mvsf va02 va04
					next
					stop
				endi
				doif from eq norn
					targ from
					mvsf va02 va04
				endi
			endi
		endi
	next
endm


*
** ELEVATOR PUSH SCRIPT - UP

scrp 3 1 36461 1
	setv va00 ov01
	seta va01 ownr
	setv va02 posy

	doif from eq pntr
		anim [3 0 3 0]
	else
		anim [2 0 2 0]
	endi

	over
	enum 3 1 36461
		doif targ ne ownr
			doif va01 eq ownr
				doif posy lt va02
					seta va01 targ
				endi
			else
				doif posy lt va02
** do nothing
				else
					seta va01 targ
				endi
			endi

			doif va01 eq targ
				setv va02 posy
			endi
		endi
	next

	targ va01
	setv va04 posx
	setv va05 posb

	doif from eq pntr
** Do nothing
	else
		targ from

		mvsf va04 va05
		doif from eq norn
			cmrp posx posy 0
		endi
	endi
endm

*
** ELEVATOR PULL SCRIPT - DOWN

scrp 3 1 36461 2
	setv va00 ov01
	seta va01 ownr
	setv va02 posy


	doif from eq pntr
		anim [3 0 3 0]
	else
		anim [1 0 1 0]
	endi
	over
	enum 3 1 36461
		doif targ ne ownr
			doif va01 eq ownr
				doif posy gt va02
					seta va01 targ
				endi
			else
				doif posy gt va02
** do nothing
				else
					seta va01 targ
				endi
			endi

			doif va01 eq targ
				setv va02 posy
			endi
		endi
	next

	targ va01
	setv va04 posx
	setv va05 posb

	targ from
	mvft va04 va05
	doif from eq norn
		cmrp posx posy 0
	endi
endm


**
**
** ======== REMOVAL SCRIPT ==========
**
**

rscr

* Door
enum 2 2 36461
	kill targ
next
* Lift
enum 3 1 36461
	kill targ
next